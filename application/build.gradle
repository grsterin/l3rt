buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

apply plugin: 'docker'

dependencies {
    compile project(":core")
    compile project(":elasticsearch-input")
    compile project(":rest")
}

ext.mainClassName = "lert.Application"

jar {
    baseName = 'l3rt'
    manifest {
        attributes "Main-Class": "$mainClassName"
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task buildDocker(type: Docker) {
    println(jar.archivePath)
    baseImage = 'java:8u111-jre'
    entryPoint(['java','-server','-jar','/opt/l3rt.jar','--config','/l3rt/config/config.json','--rules','/l3rt/rules/'])
    exposePort(8080)
    runCommand("mkdir /l3rt")
    runCommand("mkdir /l3rt/rules/")
    push = !project.hasProperty('noDockerPush')
    applicationName = 'lert'
    addFile jar.archivePath, '/opt/l3rt.jar'
    tag = "dimafeng/l3rt"
}
buildDocker.dependsOn jar